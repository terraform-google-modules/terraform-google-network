/**
 * Copyright 2026 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/******************************************
  NAT Cloud Router & NAT config
 *****************************************/

locals {
  input_regions = var.nat_config.enabled && var.nat_config.regions != null ? var.nat_config.regions : []
  nat_regions   = var.nat_config.enabled ? { for r in local.input_regions : r.name => r } : {}
  nat_ips_flat_list = flatten([
    for r in local.input_regions : [
      for i in range(r.num_addresses) : {
        region_name = r.name
        idx         = i
        key         = "${r.name}-${i}"
      }
    ]
  ])
  nat_ips_map = {
    for item in local.nat_ips_flat_list : item.key => {
      name  = item.region_name
      index = item.idx
    }
  }
}

resource "google_compute_router" "nat_router_region" {
  for_each = local.nat_regions

  name    = "cr-${var.resource_codes.short}-${var.vpc_name}-${each.key}-nat-router"
  project = var.project_id
  region  = each.key
  network = module.main.network_self_link

  bgp {
    asn = var.nat_config.bgp_asn
  }
}


resource "google_compute_address" "nat_external_addresses" {
  for_each = local.nat_ips_map

  project = var.project_id
  name    = "ca-${var.vpc_name}-${each.value.name}-${each.value.index}"
  region  = each.value.name
}

resource "google_compute_router_nat" "nat_external_addresses_region" {
  for_each = local.nat_regions

  name                   = "rn-${var.resource_codes.short}-${var.vpc_name}-${each.key}-egress"
  project                = var.project_id
  router                 = google_compute_router.nat_router_region[each.key].name
  region                 = each.key
  nat_ip_allocate_option = "MANUAL_ONLY"
  nat_ips = [
    for addr in google_compute_address.nat_external_addresses :
    addr.self_link
    if addr.region == each.key
  ]
  source_subnetwork_ip_ranges_to_nat = "ALL_SUBNETWORKS_ALL_IP_RANGES"

  log_config {
    filter = "TRANSLATIONS_ONLY"
    enable = true
  }
}
